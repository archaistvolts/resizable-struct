//! # Features
//!
//! 1. Constant time field access.
//! 1. APIs: Layout, Self and bytes.  Users can ptrCast between them.
//! 1. Computed fields.
//! 1. LSP cooperation. Avoid created/refified types which break autocomplete.
//!
//! # About
//!
//! A single buffer layout library for structs with flexible fields. User
//! specified `Layout` fields (with order determined by zig) are first in
//! memory. Flexible fields are second and sorted by descending alignment. This
//! ordering with no padding between flexible fields allows flexible offsets to
//! be calculated in constant time (see `calcOffsets()`).
//!
//! # Cached Layout API
//!
//! The core methods `calcOffsetsLayout` and `flexibleCapacities` accept a
//! Layout. Layouts are first class in addition to Self. Layout is user
//! specified and usually easier to work with than a `@Struct` refied type. This
//! library is Layout agnostic. Any zig struct will work.
//!
//! # Tradeoff
//!
//! Possible padding after fixed fields (compared with
//! [resizable-struct](https://codeberg.org/ziglang/zig/pulls/30823) where all
//! fields are ordered with descending alignment) for constant time field
//! access.
//!
//! # Buffer layout
//! Given a user `Layout` (fixed fields), the buffer format is:
//!```
//! [ fixed fields | fixed padding | flexible fields | flexible padding ]
//! ^ ALIGN                        ^ FLEX_ALIGN                         ^ ALIGN
//!```
//!
//! # Use
//!
//! A Layout decl 'pub coonst flexible_array_capacities` maps flexible
//! fields to their cached or computed capacities.
//!
//! ```zig
//! ```
//!
//! # Prior Art - References
//!
//! 1. https://tristanpemble.com/resizable-structs-in-zig/
//! 2. https://codeberg.org/ziglang/zig/pulls/30823
//!
//! # TODOS - IDEAS
//!
//! - Options
//!   - user overrides for methods such as calcOffsets.
//! - reduce binary footprint
//!     - remove inline loops and comptime params.
//! - de/serialization helpers.
//! - when fixed padding is large enough for a 'buffer_capacity' Size, add a
//! managed API with resize, resizeAssumeCapacity, resizeBounded and a hidden
//! field helper.

pub const Options = struct {
    /// size used for offset calculations and methods such as `sizeInBytes`.
    /// smaller types may be used for smaller address spaces and may be faster.
    Size: type = usize,
};

pub fn Struct(LayoutT: type, options: Options) type {
    return struct {
        /// align Self pointers so we can omit `align` attributes.
        _: void align(ALIGN),

        pub const Layout = LayoutT;
        pub const Field = meta.FieldEnum(Layout);
        pub const Size = options.Size;
        const Decl = meta.DeclEnum(Layout);
        const fields_decls = meta.fieldNames(Field) ++ meta.fieldNames(Decl);
        const FieldOrDeclInt = @Int(
            .unsigned,
            math.ceilPowerOfTwo(u16, @max(8, fields_decls.len)) catch unreachable, // u65536 should be enough fields and decls for everyone
        );
        /// an enum of Layout field names followed by decl names.
        pub const FieldOrDecl = @Enum(FieldOrDeclInt, .exhaustive, fields_decls, &simd.iota(FieldOrDeclInt, fields_decls.len));
        const layout_struct = @typeInfo(Layout).@"struct";
        const layout_fields = layout_struct.fields;
        /// user map of flexible field names to Layout field or decl names.
        const flexible_array_capacities = if (@hasDecl(Layout, "flexible_array_capacities"))
            Layout.flexible_array_capacities
        else
            .{};

        /// field bitsets in unsorted Field order.
        const layout_field_sets = sets: {
            var flexibles = enums.EnumSet(Field).initEmpty();
            var capacities = enums.EnumSet(Field).initEmpty();
            var computeds = enums.EnumSet(Decl).initEmpty();
            for (@typeInfo(@TypeOf(flexible_array_capacities)).@"struct".fields) |flexible| {
                if (!@hasField(Field, flexible.name))
                    @compileError("'" ++ flexible.name ++ "' is not a Layout field.");
                flexibles.insert(@field(Field, flexible.name));
                const capacity: FieldOrDecl = @field(flexible_array_capacities, flexible.name);
                const capname = @tagName(capacity);
                if (@hasField(Layout, capname) and @typeInfo(@FieldType(Layout, capname)) == .int)
                    capacities.insert(@field(Field, capname))
                else if (@hasDecl(Layout, capname))
                    computeds.insert(@field(Decl, capname))
                else
                    @compileError("flexible_array_capacities missing Layout field or declaration: '" ++ flexible.name ++ "'");
            }
            break :sets .{ flexibles, capacities, computeds };
        };
        const flexible_field_set = layout_field_sets[0];
        const capacity_field_set = layout_field_sets[1];

        const layout_infos = blk: {
            var maxalign: mem.Alignment = .of(Layout);
            for (layout_fields) |field| { // must user
                maxalign = maxalign.max(.fromByteUnits(AlignOf(field)));
            }
            var flexsizes: FlexSizes = undefined;
            var nextflexaligns: FlexSizes = undefined;
            for (0..NFLEX_FIELDS) |i| {
                const flexfield = flexible_field_ids[i];
                const field = layout_fields[@intFromEnum(flexfield)];
                nextflexaligns[i] = if (i < NFLEX_FIELDS - 1)
                    AlignOf(layout_fields[@intFromEnum(flexfield) + 1])
                else
                    maxalign.toByteUnits();
                flexsizes[i] = @sizeOf(ElementOf(@field(Field, field.name)));
            }

            break :blk .{ maxalign, flexsizes, nextflexaligns };
        };
        const alignment = layout_infos[0];
        const flex_field_sizes = layout_infos[1];
        /// `[2nd flexible field, ..., last flexible field, ALIGN]`.
        ///
        /// first flexible align is statically known and omitted.
        const next_flexfield_aligns = layout_infos[2];
        const flex_alignmasks = next_flexfield_aligns - @as(FlexSizesV, @splat(1));
        const FLEX_ALIGN = if (flexible_fields_sorted.len > 0) AlignOf(flexible_fields_sorted[0]) else 1;

        pub const Capacities = @Struct(
            .@"extern",
            null,
            &filterFields(StructField, .name, mapSet(layout_fields, capacity_field_set)), // TODO remove inline loops everywhere - use sorted field order.
            &@splat(Size),
            &@splat(.{}),
        );

        // # sorted fields section

        const Sort = struct {
            fn lessThanAlign(_: void, lhs: StructField, rhs: StructField) bool {
                return AlignOf(lhs) > AlignOf(rhs);
            }
        };
        /// sorted by alignment desc
        const flexible_fields_sorted = fields: {
            const fieldsraw = mapSet(layout_fields, flexible_field_set);
            var fields = fieldsraw[0..fieldsraw.len].*;
            mem.sort(StructField, &fields, {}, Sort.lessThanAlign);
            break :fields fields;
        };
        const flexible_field_ids = ids: {
            var ids: []const Field = &.{};
            for (flexible_fields_sorted) |f| {
                if (isFlexibleArray(f))
                    ids = ids ++ .{@field(Field, f.name)};
            }
            break :ids ids;
        };
        const capacity_field_ids = ids: {
            var ids: []const Field = &.{};
            for (flexible_field_ids) |field| {
                const capacity_fod = @field(flexible_array_capacities, @tagName(field));
                if (!isField(capacity_fod)) // skip computed fields
                    continue;
                const capacity = @field(Field, @tagName(capacity_fod));
                assert(capacity_field_set.contains(capacity));
                if (mem.indexOfScalar(Field, ids, capacity) == null)
                    ids = ids ++ .{capacity};
            }
            break :ids ids;
        };

        pub const ALIGN = alignment.toByteUnits();
        pub const NFIELDS = layout_fields.len;
        pub const NCAP_FIELDS = capacity_field_set.count();
        pub const NFLEX_FIELDS = flexible_field_set.count();
        pub const FIRST_FLEX_OFFSET = mem.alignForward(Size, @sizeOf(Layout), FLEX_ALIGN);

        const CapSizes = [NCAP_FIELDS]Size;
        const FlexSizes = [NFLEX_FIELDS]Size;
        const FlexSizesV = @Vector(NFLEX_FIELDS, Size);
        const Self = @This();

        /// flexible field capacities.
        pub fn flexibleCapacities(layout: *const Layout) FlexSizes {
            var capacities: FlexSizes = undefined;
            inline for (flexible_field_ids, &capacities) |flexible, *cap| {
                const capacity = @field(flexible_array_capacities, @tagName(flexible));
                cap.* = @intCast(if (comptime isField(capacity))
                    @field(layout, @tagName(capacity))
                else
                    @field(Layout, @tagName(capacity))(layout));
            }
            return capacities;
        }

        /// flexible field offsets.
        pub fn calcOffsetsFlexibleCapacities(capacities: *const FlexSizes) FlexSizes {
            const sizes = @as(FlexSizesV, capacities.*) * flex_field_sizes;
            const sizesaligned = (sizes + flex_alignmasks) & ~flex_alignmasks;
            var offsets: FlexSizes =
                @as(FlexSizesV, @splat(FIRST_FLEX_OFFSET)) +
                simd.prefixScan(.Add, 1, sizesaligned);
            if (NFLEX_FIELDS > 0)
                offsets[NFLEX_FIELDS - 1] = mem.alignForward(Size, offsets[NFLEX_FIELDS - 1], ALIGN);

            if (false and NFLEX_FIELDS > 0 and !@inComptime()) {
                std.debug.print(
                    \\
                    \\fields            {any}
                    \\flex_field_sizes  {any}
                    \\next_field_aligns {any}
                    \\capacities        {any}
                    \\sizes             {any}
                    \\sizesaligned      {any}
                    \\offsets           {any}
                    \\base,Layout size  {}, {}
                    \\
                ,
                    .{ comptime meta.tags(Field), flex_field_sizes, next_flexfield_aligns, capacities, sizes, sizesaligned, offsets, FIRST_FLEX_OFFSET, @sizeOf(Layout) },
                );
            }
            return offsets;
        }

        /// flexible field offsets.
        pub fn calcOffsetsLayout(layout: *const Layout) FlexSizes {
            return calcOffsetsFlexibleCapacities(&flexibleCapacities(layout));
        }

        /// flexible field offsets.
        pub fn calcOffsets(self: *const Self) FlexSizes {
            return calcOffsetsLayout(self.asLayout());
        }

        /// a Layout with given capacities and all other fields undefined.
        pub fn initCapacities(capacities: *const Capacities) Layout {
            var ret: Layout = undefined;
            inline for (capacity_field_ids) |field| {
                @field(ret, @tagName(field)) = @intCast(@field(capacities, @tagName(field)));
            }
            return ret;
        }

        /// size in bytes of layout. assumes layout capacities are initialized
        /// along with other fields needed by computed methods.
        pub fn sizeInBytesLayout(layout: *const Layout) Size {
            return if (NFLEX_FIELDS > 0)
                calcOffsetsLayout(layout)[NFLEX_FIELDS - 1]
            else
                FIRST_FLEX_OFFSET;
        }

        pub fn sizeInBytes(self: *const Self) Size {
            return sizeInBytesLayout(self.asLayout());
        }

        pub fn sizeInBytesCapacities(capacities: *const Capacities) Size {
            return sizeInBytesLayout(&initCapacities(capacities));
        }

        /// a buffer with size determined by layout for use with initBuffer().
        pub fn Buf(comptime layout: *const Layout) type {
            return [sizeInBytesLayout(layout)]u8;
        }

        /// a buffer with size determined by capacities for use with initBuffer().
        pub fn BufCapacities(comptime capacities: *const Capacities) type {
            return Buf(&initCapacities(capacities));
        }

        /// a copy of layout backed by buf and with flexible fields
        /// pointing into buf.
        pub fn initBuffer(buf: []align(ALIGN) u8, layout: *const Layout) !*Self {
            const ret = mem.bytesAsValue(Layout, buf);
            ret.* = layout.*;
            if (NFLEX_FIELDS == 0)
                return @ptrCast(ret);
            const offs = calcOffsetsLayout(layout);
            if (offs[offs.len - 1] > buf.len)
                return error.OutOfMemory;
            const nextoffs = simd.shiftElementsRight(offs, 1, FIRST_FLEX_OFFSET);
            inline for (flexible_field_ids, nextoffs) |flexible, nextoff| {
                const flexname = @tagName(flexible);
                const flexptr = &@field(ret, flexname);
                flexptr.* = @ptrCast(@alignCast(buf.ptr + @as(usize, @intCast(nextoff))));
                if (meta.sentinel(@FieldType(Layout, flexname))) |sentinel| { // set sentinel
                    const capacity = @field(flexible_array_capacities, flexname);
                    const capname = @tagName(capacity);
                    const len = if (@hasDecl(Layout, capname))
                        @field(Layout, capname)(ret) // computed call
                    else
                        @field(ret, capname);
                    flexptr.*[len] = sentinel;
                }
            }
            return @ptrCast(ret);
        }

        /// a struct with given capacities backed by buf and with flexible fields
        /// pointing into buf.
        pub fn initBufferCapacities(buf: []align(ALIGN) u8, capacities: *const Capacities) !*Self {
            return try initBuffer(buf, &initCapacities(capacities));
        }

        /// a Layout field or computed decl `capacity`.
        pub fn capacityOf(
            self: *const Self,
            comptime capacity: FieldOrDecl,
        ) CapacityOf(capacity) {
            const layout = self.asLayout();
            return if (@hasField(Field, @tagName(capacity)))
                @field(layout, @tagName(capacity))
            else
                @field(Layout, @tagName(capacity))(layout); // computed field call
        }

        /// unique capacity fields (no shared duplicates).
        pub fn loadCapacities(self: *const Self) CapSizes {
            const layout = self.asLayout();
            var capacities: CapSizes = undefined;
            inline for (capacity_field_ids, 0..) |field, j| {
                capacities[j] = @intCast(@field(layout, @tagName(field)));
            }
            return @bitCast(capacities);
        }

        /// one capacity per flexible field (possible shared duplicates).
        pub fn loadFlexibleCapacities(self: *const Self) FlexSizes {
            const layout = self.asLayout();
            var counts: FlexSizes = undefined;
            inline for (flexible_field_ids, 0..) |field, j| {
                counts[j] = @intCast(@field(layout, @tagName(flexibleCapacity(field))));
            }
            return @bitCast(counts);
        }

        pub fn create(allocator: mem.Allocator, capacities: *const Capacities) !*Self {
            const layout = initCapacities(capacities);
            const buf = try allocator.alignedAlloc(u8, alignment, sizeInBytesLayout(&layout));
            return initBuffer(buf, &layout);
        }

        pub fn destroy(self: *const Self, allocator: mem.Allocator) void {
            const bytes = self.asBytes();
            allocator.free(bytes[0..self.sizeInBytes()]);
        }

        /// a Layout.<field> by value.
        pub fn value(self: *const Self, comptime field: Field) @FieldType(Layout, @tagName(field)) {
            return @field(self.asLayout(), @tagName(field));
        }

        /// a const pointer to Layout.<field>.
        pub fn ptr(self: *const Self, comptime field: Field) *const @FieldType(Layout, @tagName(field)) {
            return &@field(self.asLayout(), @tagName(field));
        }

        /// a mutable pointer to Layout.<field>.
        pub fn ptrMut(self: *Self, comptime field: Field) *@FieldType(Layout, @tagName(field)) {
            return @constCast(self.ptr(field));
        }

        /// a slice of Layout.<field> with the given len.
        pub fn sliceLen(self: *const Self, comptime field: Field, len: anytype) FieldSlice(field) {
            return @ptrCast(value(self, field)[0..len]);
        }

        /// a slice of Layout.<flexible> with its capacity field.
        pub fn slice(self: *const Self, comptime flexible: Field) FieldSlice(flexible) {
            return self.sliceCapacity(flexible, flexibleCapacity(flexible));
        }

        /// slice of Layout.<field> with given capacity field or decl. capacity
        /// is a Layout unsigned integer field or a method which returns one.
        pub fn sliceCapacity(
            self: *const Self,
            comptime field: Field,
            comptime capacity: FieldOrDecl,
        ) FieldSlice(field) {
            return self.sliceLen(field, self.capacityOf(capacity));
        }

        // Self <-> Layout <-> bytes helpers

        pub fn asLayout(self: *const Self) *align(ALIGN) const Layout {
            return @ptrCast(self);
        }

        pub fn asLayoutMut(self: *Self) *align(ALIGN) Layout {
            return @ptrCast(self);
        }

        pub fn fromLayout(layout: *align(ALIGN) const Layout) *const Self {
            return @ptrCast(layout);
        }

        pub fn fromLayoutMut(layout: *align(ALIGN) Layout) *Self {
            return @ptrCast(layout);
        }

        pub fn asBytes(self: *const Self) [*]align(ALIGN) const u8 {
            return @ptrCast(self);
        }

        pub fn asBytesMut(self: *Self) [*]align(ALIGN) u8 {
            return @ptrCast(self);
        }

        pub fn fromBytes(bytes: [*]align(ALIGN) const u8) *const Self {
            return @ptrCast(bytes);
        }

        pub fn fromBytesMut(bytes: [*]align(ALIGN) u8) *Self {
            return @ptrCast(bytes);
        }

        // Self <-> Layout <-> bytes helpers // end

        /// copy field from src to dest. memcpy flexible fields and OOM if dst
        /// slice is smaller than src.
        pub fn copyField(dst: *Self, src: *const Self, comptime field: Field) mem.Allocator.Error!void {
            if (comptime flexible_field_set.contains(field)) {
                const d = dst.slice(field);
                const s = src.slice(field);
                if (s.len > d.len)
                    return error.OutOfMemory;
                @memcpy(d.ptr, s);
            } else {
                @field(dst.asLayoutMut(), @tagName(field)) = @field(src.asLayout(), @tagName(field));
            }
        }

        /// copy all fields from src to dest with `copyField`
        pub fn copy(dst: *Self, src: *const Self) mem.Allocator.Error!void {
            inline for (comptime meta.tags(Field)) |field| {
                try dst.copyField(src, field);
            }
        }

        inline fn isFlexibleArray(field: StructField) bool {
            return flexible_field_set.contains(@field(Field, field.name));
        }

        fn isField(field_or_decl: FieldOrDecl) bool {
            return @intFromEnum(field_or_decl) < NFIELDS;
        }

        fn AlignOf(field: StructField) comptime_int {
            if (isFlexibleArray(field)) {
                const pointer = @typeInfo(field.type).pointer;
                return pointer.alignment orelse @alignOf(pointer.child);
            }
            return @alignOf(field.type);
        }

        fn ElementOf(comptime field: Field) type {
            const T = @FieldType(Layout, @tagName(field));
            return if (isFlexibleArray(layout_fields[@intFromEnum(field)]))
                meta.Elem(T)
            else
                T;
        }

        inline fn flexibleCapacity(comptime flexible: Field) FieldOrDecl {
            return @field(flexible_array_capacities, @tagName(flexible));
        }

        fn CapacityOf(comptime capacity: FieldOrDecl) type {
            return if (@hasField(Field, @tagName(capacity)))
                @FieldType(Layout, @tagName(capacity))
            else
                @typeInfo(@TypeOf(@field(Layout, @tagName(capacity)))).@"fn".return_type.?;
        }

        /// a Layout.<field> slice type
        fn FieldSlice(comptime field: Field) type {
            const T = @FieldType(Layout, @tagName(field));
            const pointer = @typeInfo(T).pointer;
            return @Pointer(.slice, .{
                .@"addrspace" = pointer.address_space,
                .@"align" = pointer.alignment,
                .@"allowzero" = pointer.is_allowzero,
                .@"const" = pointer.is_const,
                .@"volatile" = pointer.is_volatile,
            }, pointer.child, pointer.sentinel());
        }

        fn fieldAttrs(comptime fields: []const StructField) [fields.len]StructField.Attributes {
            var attrs: [fields.len]std.builtin.Type.StructField.Attributes = undefined;
            for (fields, &attrs) |f, *attr| attr.* = .{
                .@"comptime" = f.is_comptime,
                .@"align" = AlignOf(f),
                .default_value_ptr = f.default_value_ptr,
            };
            return attrs;
        }
    };
}

fn filterFields(T: type, field: meta.FieldEnum(T), in: []const T) [in.len]@FieldType(T, @tagName(field)) {
    const F = @FieldType(T, @tagName(field));
    var out: [in.len]F = undefined;
    for (in, &out) |i, *o|
        o.* = @field(i, @tagName(field));
    return out;
}

fn mapSet(src: anytype, mask: anytype) []const meta.Elem(@TypeOf(src)) {
    var ret: []const meta.Elem(@TypeOf(src)) = &.{};
    for (src, 0..) |f, i| {
        if (mask.contains(@enumFromInt(i)))
            ret = ret ++ .{f};
    }
    return ret;
}

// TESTS

test Struct {
    const Model = Struct(struct {
        capacity: u8,
        flexible: [*]u8 = undefined,
        computed: [*]u8 = undefined,
        pub const flexible_array_capacities = .{
            .flexible = .capacity,
            .computed = .computedLen,
        };
        pub fn computedLen(_: *const @This()) usize {
            return 68;
        }
    }, .{});
    const Layout = Model.Layout;
    const initlayout = Layout{ .capacity = 42 };
    var buf: Model.Buf(&initlayout) align(Model.ALIGN) = undefined;
    const layout = try Model.initBuffer(&buf, &initlayout);
    try testing.expectEqual(42, layout.slice(.flexible).len);
    try testing.expectEqual(42, layout.value(.capacity));
    try testing.expectEqual(42, layout.asLayout().capacity); // Self <-> Layout
    try testing.expectEqual(42, Model.fromLayout(layout.asLayout()).slice(.flexible).len); // Self <-> Layout
    try testing.expectEqual(68, layout.slice(.computed).len); // computed

    try testPacket(usize);
    try testPacket(u32);
    try testPacket(u16);
    try testPacket(u8);
}

fn testPacket(Size: type) !void {
    const host = "ziglang.org";
    try testing.expectEqual(11, host.len);

    const PacketLayout = struct { // @sizeOf(PacketLayout)=48, Packet{ALIGN=32,FLEX_ALIGN=32},
        buf_lens: u64,
        host_len: u32,
        write_buf: [*]align(32) u8, //  [48,84) | buf_lens=20 // flexible_start
        read_buf: [*]align(16) u8, //   [96,116)
        host: [*]u8, //                 [116,127) | host_len=11
        computed: [*]u8, //             [127,127+68=195) | computedLen=68
        // flexible_start               48 = alignForward(48, FLEX_ALIGN=32)
        // buffer_end                   224 = alignForward(195, ALIGN=32)

        /// map from capacity field or decl to flexible array field
        pub const flexible_array_capacities = .{
            .write_buf = .buf_lens,
            .read_buf = .buf_lens,
            .host = .host_len,
            .computed = .computedLen,
        };

        pub fn computedLen(_: *const @This()) u8 {
            return 68;
        }
    };

    const Packet = Struct(PacketLayout, .{ .Size = Size });
    try testing.expectEqual(48, @sizeOf(PacketLayout));
    try testing.expectEqual(64, mem.alignForward(Packet.Size, @sizeOf(PacketLayout), Packet.ALIGN));
    try testing.expectEqualSlices(Packet.Field, &.{ .write_buf, .read_buf, .host, .computed }, Packet.flexible_field_ids);
    try testing.expectEqualSlices(Packet.Field, &.{ .buf_lens, .host_len }, Packet.capacity_field_ids); // computed fields excluded
    try testing.expectEqualSlices(Packet.Size, &.{ 1, 1, 1, 1 }, &Packet.flex_field_sizes);
    try testing.expectEqualSlices(Packet.Size, &.{ 16, 1, 1, 32 }, &Packet.next_flexfield_aligns);

    const capacities: Packet.Capacities = .{ .buf_lens = 20, .host_len = host.len };
    try validateLayout(Packet, Packet.initCapacities(&capacities), &.{ 96, 116, 127, 224 });
    try testing.expectEqual(64, Packet.FIRST_FLEX_OFFSET);

    const packet = try Packet.create(testing.allocator, &capacities);
    defer packet.destroy(testing.allocator);
    const layout = packet.asLayout();
    try testing.expectEqual(capacities.buf_lens, layout.buf_lens);
    try testing.expectEqual(capacities.host_len, layout.host_len);

    @memcpy(packet.slice(.host), host);
    try testing.expectEqualSlices(u8, host, packet.slice(.host));
    try testing.expectEqual(layout.write_buf[0..layout.buf_lens], packet.slice(.write_buf));
    try testing.expectEqual(layout.read_buf[0..layout.buf_lens], packet.slice(.read_buf));
    try testing.expectEqual(layout.host[0..layout.host_len], packet.slice(.host));
    try testing.expectEqual(layout.computed[0..layout.computedLen()], packet.slice(.computed));
}

fn validateLayout(
    S: type,
    comptime layout: S.Layout,
    expected_offsets: *const S.FlexSizes,
) !void {
    var buf: S.Buf(&layout) align(S.ALIGN) = undefined;
    const self = try S.initBuffer(&buf, &layout);
    try testing.expectEqualSlices(S.Size, expected_offsets, &S.calcOffsetsLayout(&layout));
    inline for (S.flexible_field_ids) |flexiblefield| {
        @memset(self.slice(flexiblefield), @intFromEnum(flexiblefield));
    }
    inline for (S.flexible_field_ids) |flexiblefield| {
        const slice = self.slice(flexiblefield);
        try testing.expectEqual(@intFromEnum(flexiblefield), slice[0]);
        try testing.expectEqual(@intFromEnum(flexiblefield), slice[slice.len - 1]);
    }
    const lensv = self.loadCapacities();
    inline for (S.capacity_field_ids, 0..) |capfield, i| {
        try testing.expectEqual(lensv[i], @field(self.asLayout(), @tagName(capfield)));
    }
}

test "zero sized, missing flexible_array_capacities" {
    try validateLayout(Struct(struct {}, .{}), .{}, &.{});
    try validateLayout(Struct(struct {
        pub const flexible_array_capacities = .{};
    }, .{}), .{}, &.{});

    try validateLayout(Struct(struct {
        a: usize = 0,
        pub const flexible_array_capacities = .{};
    }, .{}), .{}, &.{});
    try validateLayout(Struct(struct {
        a: usize = 0,
    }, .{}), .{}, &.{});
}

test "misc layouts" {
    { // over aligned fixed field
        const L = struct {
            len: u32 align(32),
            flex: [*]u8, //        [32,43) | len=11
            //                     32
            //                     64=alignForward(43,32)
            pub const flexible_array_capacities = .{ .flex = .len };
        };
        const S = Struct(L, .{});
        try testing.expectEqual(32, S.ALIGN);
        try validateLayout(S, S.initCapacities(&.{ .len = 11 }), &.{64});
    }
    { // large fixed field
        const L = struct {
            b: u64,
            a: u8,
            data: [*]u8, // [24,35)
            //              24
            //              40=alignForward(35,8)
            pub const flexible_array_capacities = .{ .data = .a };
        };
        const S = Struct(L, .{});
        try validateLayout(S, S.initCapacities(&.{ .a = 11 }), &.{40});
        try testing.expectEqualSlices(S.Field, meta.tags(S.Field), &.{ .b, .a, .data });
    }
    { // single flexible align
        const L = struct {
            b: u64,
            a: u8,
            data: [*]align(16) u8, // [24,35)
            //              24
            //              48=alignForward(35,16)
            pub const flexible_array_capacities = .{ .data = .a };
        };
        const S = Struct(L, .{});
        try validateLayout(S, S.initCapacities(&.{ .a = 11 }), &.{48});
        try testing.expectEqualSlices(S.Field, meta.tags(S.Field), &.{ .b, .a, .data });
    }
    { // extern
        const L = extern struct {
            b: u64,
            a: u8,
            data: [*]align(16) u8, // [24,35)
            //              24
            //              48=alignForward(35,16)
            pub const flexible_array_capacities = .{ .data = .a };
        };
        const S = Struct(L, .{});
        try validateLayout(S, S.initCapacities(&.{ .a = 11 }), &.{48});
        try testing.expectEqualSlices(S.Field, meta.tags(S.Field), &.{ .b, .a, .data });
    }
}

test "sentinels" {
    const Ls = .{
        struct {
            len: u8,
            sentinelptr: [*:0]u8, //   [16,27)
            //                  16
            //                  32=alignForward(16+11=27, 8)
            pub const flexible_array_capacities = .{ .sentinelptr = .len };
        },
        struct { // computed sentinel
            sentinelptr: [*:0]u8, //   [8,19)
            //                  8
            //                  24=alignForward(19, 8)
            pub fn len(_: *const @This()) u8 {
                return 11;
            }
            pub const flexible_array_capacities = .{ .sentinelptr = .len };
        },
    };
    inline for (Ls) |L| {
        const S = Struct(L, .{});
        const is_computed = @hasDecl(L, "len");
        const s = try S.create(testing.allocator, &if (is_computed) .{} else .{ .len = 11 });
        defer s.destroy(testing.allocator);
        const layout = s.asLayout();
        try testing.expectEqual([*:0]u8, @TypeOf(layout.sentinelptr));
        const slice = if (is_computed)
            layout.sentinelptr[0..layout.len() :0]
        else
            layout.sentinelptr[0..layout.len :0];
        try testing.expectEqual(11, slice.len);
        try testing.expectEqual(0, slice[11]);
    }
}

const std = @import("std");
const mem = std.mem;
const testing = std.testing;
const meta = std.meta;
const simd = std.simd;
const math = std.math;
const assert = std.debug.assert;
const enums = std.enums;
const StructField = std.builtin.Type.StructField;
const builtin = @import("builtin");
